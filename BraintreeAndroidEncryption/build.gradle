apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

android {
    compileSdkVersion 20
    buildToolsVersion "23.0.1"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 20
        versionCode 2
        versionName '2.0.0'
    }
}

/* maven deploy + signing */
task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    classifier = 'javadoc'
    from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task releaseJar(type: Jar, dependsOn: android.libraryVariants.release.javaCompile) {
    from projectDir.path + '/build/intermediates/classes/release/'
}

artifacts {
    archives releaseJar
    archives sourcesJar
    archives javadocsJar
}

signing {
    sign configurations.archives
}

def sonatypeUsername = System.properties['sonatypeUsername'] ?: ''
def sonatypePassword = System.properties['sonatypePassword'] ?: ''

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.setArtifactId 'encryption'

            pom.project {
                name 'encryption'
                groupId = 'com.braintreepayments'
                version = "${android.defaultConfig.versionName}"
                packaging 'jar'
                description 'Client side encryption for Braintree integrations on Android.'
                url 'https://github.com/braintree/braintree_android_encryption'

                scm {
                    url 'scm:git@github.com:braintree/braintree_android_encryption.git'
                    connection 'scm:git@github.com:braintree/braintree_android_encryption.git'
                    developerConnection 'scm:git@github.com:braintree/braintree_android_encryption.git'
                }

                developers {
                    developer {
                        id 'devs'
                        name 'Braintree Payments'
                    }
                }

                licenses {
                    license {
                        name 'MIT'
                        url 'http://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}
